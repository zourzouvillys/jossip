
plugins {
  id 'net.ltgt.apt-eclipse' version '0.21' apply false
}

apply plugin: 'eclipse'
apply plugin: 'maven'

allprojects { project ->

  project.group = 'io.zrz.jossip'
  project.version = version = project.hasProperty('versionId') ? project.property('versionId') : 'master'

  project.repositories {
    mavenCentral()
  }

  project.apply from: "${rootDir}/gradle/eclipse.gradle"
  project.apply plugin: 'com.jfrog.bintray'

  project.bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_APIKEY')
    // configurations = ['archives']
    publications = ['maven']
    pkg {
      repo = 'maven'
      name = 'jossip'
      userOrg = user
    }
  }

}

task configureBuildship {
  dependsOn gradle.includedBuilds*.task(':configureBuildship')
}

eclipse {
  synchronizationTasks configureBuildship
}

subprojects {
  apply plugin: "maven-publish"
  apply plugin: "java-library"
  publishing {
    publications {
      maven(MavenPublication) {
        from(components.java)
        pom {
        }
      }
    }
  }
}
