
plugins {
  id 'net.ltgt.apt-eclipse' version '0.21' apply false
}

apply plugin: 'eclipse'
apply plugin: 'maven'

allprojects { project ->

	project.group = 'com.github.zourzouvillys.jossip'
	project.version = 'master-SNAPSHOT'

	project.repositories {
	    mavenCentral()
	}

	project.apply from: "${rootDir}/gradle/eclipse.gradle"    

}

task configureBuildship {
  dependsOn gradle.includedBuilds*.task(':configureBuildship')
}

eclipse {
  synchronizationTasks configureBuildship
}

subprojects {
  apply plugin: "maven-publish"
  apply plugin: "java-library"
  publishing {
    repositories {
      maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/zourzouvillys/jossip")
        credentials {
          username = project.findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
          password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
      }
    }
    publications {
      gpr(MavenPublication) {
        from(components.java)
      }
    }
  }
}
